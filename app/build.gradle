plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.android.gms.oss-licenses-plugin'
    id "io.sentry.android.gradle" version "3.1.5"
}

def properties = new Properties()
def localKeyProperties = rootProject.file('key.properties')
if (localKeyProperties.exists()) {
    localKeyProperties.withReader('UTF-8') {
        properties.load(it)
    }
}
def sentryDsn = properties.getProperty('SENTRY_DSN')
if (sentryDsn == null) {
    sentryDsn = ""
    project.logger.error('Sentry DSN was not set!')
}
def oauthClientId = properties.getProperty('OAUTH_CLIENT_ID')
if (oauthClientId == null) {
    oauthClientId = ""
    project.logger.error('OAuth Server was not set!')
}
def oauthRedirectUrl = properties.getProperty('OAUTH_REDIRECT_URL')
if (oauthRedirectUrl == null) {
    oauthRedirectUrl = ""
    project.logger.error("OAuth Redirect URL was not set!")
}
def repoUrl = properties.getProperty('REPO_URL')
if (repoUrl == null) {
    repoUrl = ""
    project.logger.error("Repo url not set")
}
def privacyUrl = properties.getProperty('PRIVACY_URL')
if (privacyUrl == null) {
    project.logger.error("Privacy url not set")
    throw new GradleException("Privacy URL was not set! Please set the URL in the key.properties!")
}

android {
    compileSdk 33

    defaultConfig {
        def packageName = "de.hbch.traewelling"

        applicationId packageName
        minSdk 26
        targetSdk 33
        versionCode 91
        versionName "1.14.0"

        manifestPlaceholders = [
                SENTRY_DSN: sentryDsn,
                appAuthRedirectScheme: "app.traewelldroid.de"
        ]

        testInstrumentationRunner

        buildConfigField "String", "OAUTH_REDIRECT_URL", "\"${oauthRedirectUrl}\""
        buildConfigField "String", "OAUTH_CLIENT_ID", "\"${oauthClientId}\""
        buildConfigField "String", "REPO_URL", "\"${repoUrl}\""
        buildConfigField "String", "PRIVACY_URL", "\"${privacyUrl}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
        }
    }
    buildFeatures {
        dataBinding = true
        viewBinding = true
        compose = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    namespace 'de.hbch.traewelling'
}

dependencies {
    // Jetpack Compose
    def composeVersion = "1.4.3"
    implementation "androidx.compose.runtime:runtime:$composeVersion"
    implementation "androidx.compose.ui:ui:$composeVersion"
    implementation "androidx.compose.foundation:foundation:$composeVersion"
    implementation "androidx.compose.foundation:foundation-layout:$composeVersion"
    implementation "androidx.compose.material:material:$composeVersion"
    implementation "androidx.compose.runtime:runtime-livedata:$composeVersion"
    implementation "androidx.compose.ui:ui-tooling:$composeVersion"
    implementation "com.google.accompanist:accompanist-themeadapter-material3:0.28.0"
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.31.0-alpha"
    implementation "com.google.accompanist:accompanist-permissions:0.31.1-alpha"
    implementation "androidx.compose.material3:material3:1.1.1"
    implementation "androidx.activity:activity-compose:1.7.2"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.fragment:fragment-ktx:1.6.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.browser:browser:1.5.0"
    implementation 'androidx.core:core-splashscreen:1.0.1'
    implementation 'com.auth0.android:jwtdecode:2.0.1'
    implementation "net.openid:appauth:0.11.1"
    implementation 'org.greenrobot:eventbus:3.3.1'

    // OSM integration
    implementation 'org.osmdroid:osmdroid-android:6.1.10'

    // Swipe to refresh
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    // Secure Storage
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.jcloquell:androidsecurestorage:0.1.3'

    // Navigation Component
    def nav_version_ktx = "2.7.0-beta01"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version_ktx"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version_ktx"

    // Retrofit
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'

    // Sentry
    implementation 'io.sentry:sentry-android:6.4.1'

    // Coil for image loading
    implementation 'io.coil-kt:coil:2.3.0'
    implementation "io.coil-kt:coil-compose:2.3.0"

    // Charts
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation "com.patrykandpatrick.vico:compose-m3:1.6.6"

    // Emoji pack support
    implementation 'de.c1710:filemojicompat-autoinit:3.2.5'

    // OSS licenses
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.1'

    // Shortcuts
    implementation 'androidx.core:core-google-shortcuts:1.1.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}