plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
    id "androidx.navigation.safeargs.kotlin"
    id "io.sentry.android.gradle" version "3.12.0"
    id "com.mikepenz.aboutlibraries.plugin"
    id "com.google.gms.google-services"
}

sentry {
    autoInstallation {
        enabled = false
    }

    ignoredFlavors = ["foss"]
}

aboutLibraries {
    excludeFields = ["generated"]
}

android {
    def packageName = "de.hbch.traewelling"
    def name = "2.6.0"
    def code = 139

    compileSdk 34

    defaultConfig {
        applicationId packageName
        minSdk 26
        targetSdk 34
        versionCode code
        versionName name

        manifestPlaceholders["appAuthRedirectScheme"] = "app.traewelldroid.de"

        testInstrumentationRunner
    }

    signingConfigs {
        release {
            try {
                def keystorePropertiesFile = rootProject.file("keystore.properties")
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            } catch (ignored) { }
        }
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            minifyEnabled false

            buildConfigField "String", "OAUTH_REDIRECT_URL", "\"https://app.traewelldroid.de/oauth2redirect\""
            buildConfigField "String", "OAUTH_CLIENT_ID", "\"43\""
            buildConfigField "String", "REPO_URL", "\"https://github.com/Traewelldroid/traewelldroid\""
            buildConfigField "String", "PRIVACY_URL", "\"https://traewelldroid.de/privacy\""
            buildConfigField "String", "UNLEASH_URL", "\"https://unleash.traewelldroid.de/api/frontend\""
            buildConfigField "String", "UNLEASH_KEY", "\"*:production.35c591bf9e4fd8eada44bc1d51fe5261e407779280ddf3dec0e00d68\""
            buildConfigField "String", "WEBHOOK_URL", "\"https://webhook.traewelldroid.de\""
            buildConfigField "String", "UP_FCM_PROXY", "\"https://push.traewelldroid.de/FCM\""
            manifestPlaceholders["SENTRY_DSN"] = "https://9664595fcad94b6c90fe2021ab1ec3f0@sentry.traewelldroid.de/3"

            signingConfig signingConfigs.release
        }
        debug {
            buildConfigField "String", "OAUTH_REDIRECT_URL", "\"https://app.traewelldroid.de/oauth2redirect\""
            buildConfigField "String", "OAUTH_CLIENT_ID", "\"63\""
            buildConfigField "String", "REPO_URL", "\"https://github.com/Traewelldroid/traewelldroid\""
            buildConfigField "String", "PRIVACY_URL", "\"https://traewelldroid.de/privacy\""
            buildConfigField "String", "UNLEASH_URL", "\"https://unleash.traewelldroid.de/api/frontend\""
            buildConfigField "String", "UNLEASH_KEY", "\"*:development.ce069ae01e960f412f694c4a3afc3bd3e4dccbb21102c8eb36e99781\""
            buildConfigField "String", "WEBHOOK_URL", "\"https://webhooktest.traewelldroid.de\""
            buildConfigField "String", "UP_FCM_PROXY", "\"https://push.traewelldroid.de/FCM\""
            manifestPlaceholders["SENTRY_DSN"] = "https://8421286d211d475cb0343fd1aff3fc06@sentry.traewelldroid.de/2"
        }
    }
    buildFeatures {
        dataBinding = true
        viewBinding = true
        compose = true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.1"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    namespace "de.hbch.traewelling"
    flavorDimensions = [ "libs" ]
    productFlavors {
        play {
            dimension "libs"
            versionNameSuffix "-play"
            apply plugin: "com.google.gms.google-services"
        }
        foss {
            dimension "libs"
            // versionNameSuffix "-foss"
        }
    }
}

dependencies {
    // Jetpack Compose
    def composeVersion = "1.5.0"
    implementation "androidx.compose.runtime:runtime:$composeVersion"
    implementation "androidx.compose.ui:ui:$composeVersion"
    implementation "androidx.compose.foundation:foundation:$composeVersion"
    implementation "androidx.compose.foundation:foundation-layout:$composeVersion"
    implementation "androidx.compose.material:material:$composeVersion"
    implementation "androidx.compose.runtime:runtime-livedata:1.6.1"
    implementation "androidx.compose.ui:ui-tooling:$composeVersion"
    implementation "com.google.accompanist:accompanist-themeadapter-material3:0.28.0"
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.30.1"
    implementation "com.google.accompanist:accompanist-permissions:0.30.1"
    implementation "androidx.compose.material3:material3:1.2.0"
    implementation "androidx.activity:activity-compose:1.8.2"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"

    implementation "androidx.core:core-ktx:1.12.0"
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "com.google.android.material:material:1.11.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.7.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.7.0"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.browser:browser:1.7.0"
    implementation "androidx.core:core-splashscreen:1.0.1"
    implementation "com.auth0.android:jwtdecode:2.0.2"
    implementation "net.openid:appauth:0.11.1"
    implementation "org.greenrobot:eventbus:3.3.1"

    // OSM integration
    implementation "org.osmdroid:osmdroid-android:6.1.16"

    // Secure Storage
    implementation "com.google.code.gson:gson:2.10.1"
    implementation "com.jcloquell:androidsecurestorage:0.1.3"

    // Navigation Component
    implementation "androidx.navigation:navigation-compose:2.7.7"

    // Retrofit
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"

    // Coil for image loading
    implementation "io.coil-kt:coil-compose:2.4.0"

    // Emoji pack support
    implementation "de.c1710:filemojicompat-autoinit:3.2.5"

    // OSS licenses
    implementation "com.mikepenz:aboutlibraries-compose:10.8.3"

    // Feature flags
    implementation "io.getunleash:unleash-android-proxy-sdk:0.5.5"

    // Sentry
    playImplementation "io.sentry:sentry-android:6.28.0"

    // Unified Push
    implementation 'com.github.UnifiedPush:android-connector:2.1.1'
    playImplementation('com.github.UnifiedPush:android-embedded_fcm_distributor:2.2.0') {
        exclude group: 'com.google.firebase', module: 'firebase-core'
        exclude group: 'com.google.firebase', module: 'firebase-analytics'
        exclude group: 'com.google.firebase', module: 'firebase-measurement-connector'
    }

    // CSV reader
    implementation 'com.github.doyaaaaaken:kotlin-csv-jvm:1.9.2'

    // Reviews
    playImplementation 'com.google.android.play:review:2.0.1'
    playImplementation 'com.google.android.play:review-ktx:2.0.1'
}


if (getGradle().getStartParameter().getTaskNames().stream().anyMatch {f -> f.contains("Play") }) {
    println("Play services will be applied!")
} else {
    println("Play services were not applied!")
}
