plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.android.gms.oss-licenses-plugin'
    id "io.sentry.android.gradle" version "3.11.1"
}

def sentryDsn = ""
def oauthClientId = ""
def oauthRedirectUrl = ""
def repoUrl = ""
def privacyUrl = ""
def unleashUrl = ""
def unleashKey = ""

ext.loadProperties = { String file ->
    def props = new Properties()
    def _file = rootProject.file(file)
    if (_file.exists()) {
        _file.withReader('UTF-8') {
            props.load(it)
        }
    }

    sentryDsn = props.getProperty('SENTRY_DSN', "")
    repoUrl = props.getProperty('REPO_URL', "")
    unleashUrl = props.getProperty('UNLEASH_URL', "")
    unleashKey = props.getProperty('UNLEASH_KEY', "")

    // Mandatory
    privacyUrl = props.getProperty('PRIVACY_URL')
    oauthClientId = props.getProperty('OAUTH_CLIENT_ID')
    oauthRedirectUrl = props.getProperty('OAUTH_REDIRECT_URL')

    if (privacyUrl == null || oauthClientId == null || oauthRedirectUrl == null) {
        throw new GradleException("Mandatory property missing!")
    }
}

android {
    compileSdk 33

    defaultConfig {
        def packageName = "de.hbch.traewelling"

        applicationId packageName
        minSdk 26
        targetSdk 33
        versionCode 107
        versionName "1.21.2"

        manifestPlaceholders["appAuthRedirectScheme"] = "app.traewelldroid.de"

        testInstrumentationRunner
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            minifyEnabled false

            loadProperties("key.properties")
            buildConfigField "String", "OAUTH_REDIRECT_URL", "\"${oauthRedirectUrl}\""
            buildConfigField "String", "OAUTH_CLIENT_ID", "\"${oauthClientId}\""
            buildConfigField "String", "REPO_URL", "\"${repoUrl}\""
            buildConfigField "String", "PRIVACY_URL", "\"${privacyUrl}\""
            buildConfigField "String", "UNLEASH_URL", "\"${unleashUrl}\""
            buildConfigField "String", "UNLEASH_KEY", "\"${unleashKey}\""
            manifestPlaceholders["SENTRY_DSN"] = sentryDsn
        }
        debug {
            loadProperties("dev.properties")
            buildConfigField "String", "OAUTH_REDIRECT_URL", "\"${oauthRedirectUrl}\""
            buildConfigField "String", "OAUTH_CLIENT_ID", "\"${oauthClientId}\""
            buildConfigField "String", "REPO_URL", "\"${repoUrl}\""
            buildConfigField "String", "PRIVACY_URL", "\"${privacyUrl}\""
            buildConfigField "String", "UNLEASH_URL", "\"${unleashUrl}\""
            buildConfigField "String", "UNLEASH_KEY", "\"${unleashKey}\""
            manifestPlaceholders["SENTRY_DSN"] = sentryDsn
        }
    }
    buildFeatures {
        dataBinding = true
        viewBinding = true
        compose = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    namespace 'de.hbch.traewelling'
}

dependencies {
    // Jetpack Compose
    def composeVersion = "1.4.3"
    implementation "androidx.compose.runtime:runtime:$composeVersion"
    implementation "androidx.compose.ui:ui:$composeVersion"
    implementation "androidx.compose.foundation:foundation:$composeVersion"
    implementation "androidx.compose.foundation:foundation-layout:$composeVersion"
    implementation "androidx.compose.material:material:$composeVersion"
    implementation "androidx.compose.runtime:runtime-livedata:$composeVersion"
    implementation "androidx.compose.ui:ui-tooling:$composeVersion"
    implementation "com.google.accompanist:accompanist-themeadapter-material3:0.28.0"
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.30.1"
    implementation "com.google.accompanist:accompanist-permissions:0.30.1"
    implementation "androidx.compose.material3:material3:1.1.1"
    implementation "androidx.activity:activity-compose:1.7.2"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.browser:browser:1.5.0"
    implementation 'androidx.core:core-splashscreen:1.0.1'
    implementation 'com.auth0.android:jwtdecode:2.0.1'
    implementation "net.openid:appauth:0.11.1"
    implementation 'org.greenrobot:eventbus:3.3.1'

    // OSM integration
    implementation 'org.osmdroid:osmdroid-android:6.1.10'

    // Secure Storage
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.jcloquell:androidsecurestorage:0.1.3'

    // Navigation Component
    def nav_version_ktx = "2.6.0"
    implementation "androidx.navigation:navigation-compose:$nav_version_ktx"

    // Retrofit
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'

    // Coil for image loading
    implementation "io.coil-kt:coil-compose:2.3.0"

    // Emoji pack support
    implementation 'de.c1710:filemojicompat-autoinit:3.2.5'

    // OSS licenses
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.1'

    // Shortcuts
    implementation 'androidx.core:core-google-shortcuts:1.1.0'

    // Feature flags
    implementation "io.getunleash:unleash-android-proxy-sdk:0.5.5"
}