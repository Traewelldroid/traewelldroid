plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.android.gms.oss-licenses-plugin'
    id "io.sentry.android.gradle" version "3.1.5"
}

def properties = new Properties()
def localKeyProperties = rootProject.file('key.properties')
if (localKeyProperties.exists()) {
    localKeyProperties.withReader('UTF-8') {
        properties.load(it)
    }
}
def sentryDsn = properties.getProperty('SENTRY_DSN')
if (sentryDsn == null) {
    sentryDsn = ""
    project.logger.error('Sentry DSN was not set!')
}
def oauthServer = properties.getProperty('OAUTH_SERVER')
if (oauthServer == null) {
    oauthServer = ""
    project.logger.error('OAuth Server was not set!')
}
def repoUrl = properties.getProperty('REPO_URL')
if (repoUrl == null) {
    repoUrl = ""
    project.logger.error("Repo url not set")
}
def privacyUrl = properties.getProperty('PRIVACY_URL')
if (privacyUrl == null) {
    project.logger.error("Privacy url not set")
    throw new GradleException("Privacy URL was not set! Please set the URL in the key.properties!")
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "de.hbch.traewelling"
        minSdk 26
        targetSdk 33
        versionCode 79
        versionName "1.8.2"

        manifestPlaceholders = [
                SENTRY_DSN: sentryDsn
        ]

        testInstrumentationRunner

        buildConfigField "android.net.Uri", "OAUTH_SERVER", "android.net.Uri.parse(\"${oauthServer}\")"
        buildConfigField "String", "REPO_URL", "\"${repoUrl}\""
        buildConfigField "String", "PRIVACY_URL", "\"${privacyUrl}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
        }
    }
    buildFeatures {
        dataBinding = true
        viewBinding = true
        compose = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    namespace 'de.hbch.traewelling'
}

dependencies {
    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }

    // Jetpack Compose

    implementation "androidx.compose.runtime:runtime:1.4.1"
    implementation "androidx.compose.ui:ui:1.4.1"
    implementation "androidx.compose.foundation:foundation:1.4.1"
    implementation "androidx.compose.foundation:foundation-layout:1.4.1"
    implementation "androidx.compose.material:material:1.4.1"
    implementation "androidx.compose.runtime:runtime-livedata:1.4.1"
    implementation "androidx.compose.ui:ui-tooling:1.4.1"
    implementation "com.google.accompanist:accompanist-themeadapter-material3:0.28.0"
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.31.0-alpha"
    implementation "androidx.compose.material3:material3:1.0.1"
    implementation "androidx.activity:activity-compose:1.7.0"

    implementation 'androidx.core:core-ktx:1.10.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.fragment:fragment-ktx:1.5.6'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.browser:browser:1.5.0"
    implementation 'androidx.core:core-splashscreen:1.0.0'
    implementation 'com.auth0.android:jwtdecode:2.0.1'
    implementation 'org.greenrobot:eventbus:3.3.1'

    // OSM integration
    implementation 'org.osmdroid:osmdroid-android:6.1.10'

    // Swipe to refresh
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    // Secure Storage
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.jcloquell:androidsecurestorage:0.1.3'

    // Navigation Component
    def nav_version_ktx = "2.6.0-alpha09"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version_ktx"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version_ktx"

    // Retrofit
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'

    // Sentry
    implementation 'io.sentry:sentry-android:6.4.1'

    // Coil for image loading
    implementation 'io.coil-kt:coil:2.1.0'

    // Charts
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'

    // Emoji pack support
    implementation 'de.c1710:filemojicompat-autoinit:3.2.5'

    // OSS licenses
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'

    // Shortcuts
    implementation 'androidx.core:core-google-shortcuts:1.1.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}