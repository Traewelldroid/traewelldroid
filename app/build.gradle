plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.android.gms.oss-licenses-plugin'
    id "io.sentry.android.gradle" version "3.1.5"
}

def properties = new Properties()
def localKeyProperties = rootProject.file('key.properties')
if (localKeyProperties.exists()) {
    localKeyProperties.withReader('UTF-8') {
        properties.load(it)
    }
}
def sentryDsn = properties.getProperty('SENTRY_DSN')
if (sentryDsn == null) {
    sentryDsn = ""
    project.logger.error('Sentry DSN was not set!')
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "de.hbch.traewelling"
        minSdk 23
        targetSdk 33
        versionCode 62
        versionName "1.0.2"

        manifestPlaceholders = [
                SENTRY_DSN: sentryDsn
        ]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        dataBinding = true
        viewBinding = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.8.0-alpha01'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.fragment:fragment-ktx:1.5.3'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.core:core-splashscreen:1.0.0'
    implementation 'com.auth0.android:jwtdecode:2.0.1'

    // OSM integration
    implementation 'org.osmdroid:osmdroid-android:6.1.10'

    // Swipe to refresh
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    // Secure Storage
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'com.jcloquell:androidsecurestorage:0.1.3'
    implementation 'androidx.compose.material:material:1.3.0-beta03'

    // Navigation Component
    def nav_version_ktx = "2.5.2"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version_ktx"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version_ktx"

    // Retrofit
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'

    // Sentry
    implementation 'io.sentry:sentry-android:6.4.1'

    // Coil for image loading
    implementation 'io.coil-kt:coil:1.4.0'

    // Charts
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'

    // Emoji pack support
    implementation 'de.c1710:filemojicompat-autoinit:3.2.5'

    // OSS licenses
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'

    // Shortcuts
    implementation "androidx.core:core-ktx:1.9.0"
    implementation 'androidx.core:core-google-shortcuts:1.0.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}